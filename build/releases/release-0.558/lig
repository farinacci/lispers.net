#!/bin/tcsh
# -----------------------------------------------------------------------------
#             
# Copyright 2013-2019 lispers.net - Dino Farinacci <farinacci@gmail.com>
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#     http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.    
# 
# -----------------------------------------------------------------------------
#
# lig
#
# Just a wrapper for lisp-lig where it prompts for parameters when EID is
# is not supplied.
# 
# -----------------------------------------------------------------------------

set EID = "$1"
set ASK = 0
if ("$EID" == "") then
    set ASK = 1
    echo -n "Enter EID name or address: "
    set EID = "$<"
endif
set MR = $2
if ($MR == "") then
    echo -n "Enter Map-Resolver name or RLOC address: "
    set MR = $<
endif

#
# Ask for other parmeters.
#
set PARMS = ""
if ($ASK == 1) then
    echo -n "Enter source-EID (return means no source EID specified): "
    set SEID = $<
    if ($SEID != "") then
        set PARMS = "source $SEID"
    endif

    echo -n "Enter request count: "
    set COUNT = $<
    if ($COUNT != "") then
        set PARMS = "$PARMS count $COUNT"
    endif

    echo -n "Enter debug mode (y/n): "
    set DEBUG = $<
    if ($DEBUG == "y") then
        set PARMS = "$PARMS debug"
    endif

    echo -n "Suppress Info-Requests for NAT-traversal (y/n): "
    set NAT = $<
    if ($NAT == "y") then
        set PARMS = "$PARMS no-info"
    endif

    echo -n "Enter PubSub mode (y/n): "
    set PUBSUB = $<
    if ($PUBSUB == "y") then
        set PARMS = "$PARMS pubsub"
    endif
endif

set PARMS = "to $MR $PARMS"
echo "lig command parameters are: $EID $PARMS"
echo ""

set FLAGS = "-W ignore -O -u"
if (-f ./lisp-lig.py) then
    sudo python $FLAGS ./lisp-lig.py "$EID" $PARMS
else
    sudo python $FLAGS ./lisp-lig.pyo "$EID" $PARMS
endif
